export CUDA_NAME := "cu124"
export CUDA_VERSION := "12.4.1"
export BASE_VERSION := "0.5.2"
export VERSION := "0.0.1"

export REPOSITORY := "docker.texttechnologylab.org"
export ANNOTATOR_NAME := "duui-slc-biaffine"

build version=VERSION:
    docker build \
        --build-arg CUDA_NAME="{{CUDA_NAME}}" \
        --build-arg CUDA_VERSION="{{CUDA_VERSION}}" \
        --build-arg BASE_VERSION="{{BASE_VERSION}}" \
        --build-arg VERSION="{{version}}" \
        -t {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{version}} \
        -f src/main/docker/Dockerfile src/main/

test version=VERSION:
    docker container ls | grep {{ANNOTATOR_NAME}}-test && docker stop {{ANNOTATOR_NAME}}-test && docker rm {{ANNOTATOR_NAME}}-test || echo ""
    docker run --rm --name {{ANNOTATOR_NAME}}-test -d -p 9714:9714 {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{version}}
    sleep 2 && mvn test -Dversion={{version}} || echo "Tests failed"
    docker stop {{ANNOTATOR_NAME}}-test

tag version=VERSION as="latest":
    docker tag {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{version}} {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{as}}

push version=VERSION:
    docker push {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{version}}